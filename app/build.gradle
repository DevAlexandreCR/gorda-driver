plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'com.google.gms.google-services'
    id 'com.google.android.libraries.mapsplatform.secrets-gradle-plugin'
}

android {
    compileSdk 35

    defaultConfig {
        applicationId "gorda.driver"
        minSdk 24
        targetSdk 35
        versionCode 70
        versionName "1.2.4"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        // Generate build date at compile time
        buildConfigField "String", "BUILD_DATE", "\"${new Date().format('MMM dd, yyyy')}\""
        buildConfigField "long", "BUILD_TIMESTAMP", "${System.currentTimeMillis()}L"
    }

    flavorDimensions += "brand"

    def baseVersionCode = 70
    def versionCodeSpacing = 1_000_000

    signingConfigs {
        redblanca {
            if (project.hasProperty("RedBlancaStoreFile")) {
                storeFile file(project.property("RedBlancaStoreFile"))
            }
            if (project.hasProperty("RedBlancaStorePassword")) {
                storePassword project.property("RedBlancaStorePassword")
            }
            if (project.hasProperty("RedBlancaKeyAlias")) {
                keyAlias project.property("RedBlancaKeyAlias")
            }
            if (project.hasProperty("RedBlancaKeyPassword")) {
                keyPassword project.property("RedBlancaKeyPassword")
            }
        }

        demo {
            if (project.hasProperty("DemoStoreFile")) {
                storeFile file(project.property("DemoStoreFile"))
            }
            if (project.hasProperty("DemoStorePassword")) {
                storePassword project.property("DemoStorePassword")
            }
            if (project.hasProperty("DemoKeyAlias")) {
                keyAlias project.property("DemoKeyAlias")
            }
            if (project.hasProperty("DemoKeyPassword")) {
                keyPassword project.property("DemoKeyPassword")
            }
        }
    }

    productFlavors {
        redblanca {
            dimension "brand"
            applicationId "gorda.driver"
            versionCode baseVersionCode
            manifestPlaceholders = [
                    appAuthRedirectScheme: "gorda",
                    deeplinkHost        : "driver.redblanca.app",
                    deeplinkScheme      : "https",
                    appName             : "Red Blanca",
                    MAPS_API_KEY        : (project.findProperty("MAPS_API_KEY") ?: "")
            ]
            buildConfigField "String", "BRAND_NAME", '"Red Blanca"'
            buildConfigField "String", "SUPPORT_EMAIL", '"driver@redblanca.com.co"'
            buildConfigField "String", "BASE_URL", '"https://api.redblanca.com"'
            buildConfigField "String", "PRIVACY_URL", '"https://www.termsfeed.com/live/0f4bfadc-6c3b-48a4-b45a-6583d65c0764"'
            buildConfigField "String", "TERMS_URL", '"https://www.termsfeed.com/live/0f4bfadc-6c3b-48a4-b45a-6583d65c0764"'
        }

        demo {
            dimension "brand"
            applicationId "gorda.demo"
            versionCode versionCodeSpacing + baseVersionCode
            manifestPlaceholders = [
                    appAuthRedirectScheme: "gordademo",
                    deeplinkHost        : "demo.gorda.app",
                    deeplinkScheme      : "https",
                    appName             : "Gorda Demo",
                    MAPS_API_KEY        : (project.findProperty("MAPS_API_KEY_DEMO") ?: (project.findProperty("MAPS_API_KEY") ?: ""))
            ]
            buildConfigField "String", "BRAND_NAME", '"Gorda Demo"'
            buildConfigField "String", "SUPPORT_EMAIL", '"support@gorda.demo"'
            buildConfigField "String", "BASE_URL", '"https://api.demo.gorda.app"'
            buildConfigField "String", "PRIVACY_URL", '"https://demo.gorda.app/privacy"'
            buildConfigField "String", "TERMS_URL", '"https://demo.gorda.app/terms"'
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            productFlavors.all { flavor ->
                if (flavor.name == "redblanca") {
                    signingConfig signingConfigs.redblanca
                } else if (flavor.name == "demo") {
                    signingConfig signingConfigs.demo
                }
            }
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8.toString()
    }
    buildFeatures {
        viewBinding true
        buildConfig true
    }
    packagingOptions {
        jniLibs {
            useLegacyPackaging = false
        }
    }
    namespace 'gorda.driver'
}

androidComponents {
    onVariants(selector().withBuildType("release")) { variant ->
        def flavorName = variant.productFlavors.find { it.first == "brand" }?.second ?: variant.flavorName ?: "redblanca"
        def signing = project.android.signingConfigs.findByName(flavorName) ?: project.android.signingConfigs.findByName("redblanca")
        if (signing != null) {
            variant.signingConfig?.setConfig(signing)
        }
    }
}

dependencies {
    implementation 'androidx.recyclerview:recyclerview:1.3.0'
    modules {
        module("org.jetbrains.kotlin:kotlin-stdlib-jdk7") {
            replacedBy("org.jetbrains.kotlin:kotlin-stdlib", "kotlin-stdlib-jdk7 is now part of kotlin-stdlib")
        }
        module("org.jetbrains.kotlin:kotlin-stdlib-jdk8") {
            replacedBy("org.jetbrains.kotlin:kotlin-stdlib", "kotlin-stdlib-jdk8 is now part of kotlin-stdlib")
        }
    }
    implementation platform('com.google.firebase:firebase-bom:30.1.0')
    implementation 'com.google.android.gms:play-services-location:21.0.1'
    implementation 'androidx.core:core-ktx:1.10.1'
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'com.google.android.material:material:1.9.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation 'androidx.navigation:navigation-fragment-ktx:2.5.3'
    implementation 'androidx.navigation:navigation-ui-ktx:2.5.3'
    implementation 'com.google.firebase:firebase-database-ktx:20.2.2'
    implementation 'com.google.firebase:firebase-firestore-ktx:24.11.1'
    implementation 'com.google.firebase:firebase-auth-ktx:22.1.1'
    implementation 'com.firebaseui:firebase-ui-auth:8.0.1'
    implementation 'androidx.lifecycle:lifecycle-livedata-ktx:2.6.1'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.6.1'
    implementation 'com.google.firebase:firebase-storage-ktx:20.2.1'
    implementation 'com.github.bumptech.glide:glide:4.13.2'
    implementation 'com.google.android.gms:play-services-maps:18.1.0'
    implementation "com.squareup.picasso:picasso:2.71828"
    implementation "com.squareup.retrofit2:retrofit:2.9.0"
    implementation "com.squareup.retrofit2:converter-gson:2.9.0"
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.6.4'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'com.google.firebase:firebase-messaging:23.2.1'
    implementation 'com.google.firebase:firebase-core:21.1.1'
    implementation 'androidx.preference:preference-ktx:1.2.1'
    annotationProcessor 'com.github.bumptech.glide:compiler:4.13.2'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
}